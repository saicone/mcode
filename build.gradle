subprojects {
    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'idea'

    idea {
        module {
            downloadJavadoc = true
            downloadSources = true
        }
    }

    repositories {
        maven { url 'https://jitpack.io' }
        mavenCentral()
    }

    dependencies {
        compileOnly libs.annotations
    }

    compileJava {
        options.encoding = 'UTF-8'
    }

    java {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
        withSourcesJar()
        withJavadocJar()
    }

    publishing {
        publications {
            maven(MavenPublication) {
                groupId project.group
                artifactId project.name
                version project.version
                from components.java
            }
        }
    }
}

def javadocProjects= [
        ':common',
        // Modules
        'module:module-command',
        'module:module-command-brigadier',
        'module:module-lang',
        'module:module-lang-adventure',
        'module:module-script',
        'module:module-script-js',
        'module:module-task',
        // Platforms
        'platform:platform-bukkit',
        'platform:platform-bungee',
        'platform:platform-velocity',
        // Extensions
        'extension:extension-cache-set',
        'extension:extension-lookup'
]

task allJavadoc(type: Javadoc) {
    source javadocProjects.collect { project(it).sourceSets.main.allJava }
    classpath = files(javadocProjects.collect { project(it).sourceSets.main.compileClasspath })
    options {
        // Java
        links 'https://docs.oracle.com/en/java/javase/11/docs/api/'
        // Libraries
        links 'https://javadoc.io/doc/com.google.code.gson/gson/latest/'
        links 'https://javadoc.io/doc/com.google.guava/guava/latest/'
        links 'https://javadoc.io/doc/com.github.ben-manes.caffeine/caffeine/latest/'
        links 'https://javadoc.io/doc/org.jetbrains/annotations/latest/'

        links 'https://jd.advntr.dev/api/latest/'
        links 'https://jd.advntr.dev/text-minimessage/latest/'
        links 'https://jd.advntr.dev/text-serializer-legacy/latest/'
        links 'https://jd.advntr.dev/platform/bukkit/latest/'
        links 'https://jd.advntr.dev/platform/bungeecord/latest/'

        links 'https://javadoc.saicone.com/types/'
        links 'https://javadoc.saicone.com/settings/'
        links 'https://javadoc.saicone.com/delivery4j/'

        // Platforms
        links 'https://hub.spigotmc.org/javadocs/bukkit/'
        links 'https://javadoc.io/doc/net.md-5/bungeecord-api/latest/'
        links 'https://jd.papermc.io/velocity/3.3.0/'
    }
    destinationDir = file(project.layout.buildDirectory.dir('docs/all-javadoc'))
}